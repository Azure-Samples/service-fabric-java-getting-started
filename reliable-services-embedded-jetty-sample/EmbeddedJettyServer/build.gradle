apply plugin: 'java'
apply plugin: 'eclipse'

sourceSets {
  main {
     java.srcDirs = ['src']
      resources {
       srcDirs = ['src']
     }
   }
}

clean.doFirst {
    delete "${projectDir}/lib"
    delete "${projectDir}/out"
    delete "${projectDir}/../EmbeddedJettyApplication/EmbeddedJettyServerPkg/Code/lib"
    delete "${projectDir}/../EmbeddedJettyApplication/EmbeddedJettyServerPkg/Code/EmbeddedJettyServer.jar"
}

repositories {
    mavenCentral()
}

configurations {
    azuresf
}

dependencies {
    azuresf ('com.microsoft.servicefabric:sf-actors:1.0.1')
    azuresf ('org.eclipse.jetty:jetty-server:9.2.15.v20160210')
    azuresf ('org.eclipse.jetty:jetty-servlet:9.2.15.v20160210')
    compile fileTree(dir: 'lib', include: '*.jar')
}

task explodeDeps(type: Copy, dependsOn:configurations.azuresf) { task ->
    configurations.azuresf.filter { it.toString().contains("native") }.each{
        from zipTree(it)
    }
    configurations.azuresf.filter { !it.toString().contains("native") }.each {
        from it
    }
    into "lib"
    include "lib*.so", "*.jar"
}

jar {
    manifest {
    def mpath = configurations.azuresf.collect {'lib/'+it.getName()}.join (' ')
    attributes(
        'Main-Class': 'statelessservice.EmbeddedJettyServerServiceHost',
        "Class-Path": mpath)
    baseName "EmbeddedJettyServer"

    destinationDir = file('../EmbeddedJettyApplication/EmbeddedJettyServerPkg/Code/')
    }
}

compileJava.dependsOn(explodeDeps)

task copyDeps{
    doLast{
        copy {
            from("lib/")
            into("../EmbeddedJettyApplication/EmbeddedJettyServerPkg/Code/lib")
            include('*')
        }
    }
}
defaultTasks 'clean', 'jar', 'copyDeps'
